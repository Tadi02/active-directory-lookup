plugins {
    id 'java'
    id 'application'

    id 'findbugs'
    id 'pmd'
    id 'jacoco'

    id 'maven-publish'
    id 'net.researchgate.release' version '2.3.4'
    id "com.jfrog.bintray" version '1.4'
}

mainClassName = "io.github.kdabir.adl.cli.ActiveDirectoryCLI"

sourceSets {
    integrationTest {
        java.srcDir 'src/integrationTest/java'
        resources.srcDir 'src/integrationTest/resources'
        compileClasspath += sourceSets.main.runtimeClasspath
    }
}

repositories { jcenter() }

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-all:1.10.19'

    integrationTestCompile 'junit:junit:4.12'
}


task integrationTests(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath += sourceSets.integrationTest.runtimeClasspath
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

jacocoTestReport {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

bintray {
    user = project.properties.get "bintrayUser"
    key = project.properties.get "bintrayKey"
    publications = ['mavenJava']

    def projectName = project.name
    def projectDescription = project.description

    pkg {
        repo = 'maven'
        name = projectName
        desc = projectDescription
        licenses = ['MIT']
        vcsUrl = 'https://github.com/kdabir/active-directory-lookup.git'
    }
}

afterReleaseBuild.dependsOn tasks.bintrayUpload


task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
}
