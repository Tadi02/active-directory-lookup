apply plugin: 'java'
apply plugin: 'application'

apply plugin: 'findbugs'
apply plugin: 'pmd'
apply plugin: "jacoco"


group = "io.github.kdabir.adl"
version = "0.1-SNAPSHOT"
description = "A Java Library/Application to access Active Directory"
mainClassName = "io.github.kdabir.adl.cli.ActiveDirectoryCLI"

repositories {
    jcenter()
}

sourceSets {
    integrationTest {
        java {
            srcDir 'src/integrationTest/java'
        }
        resources {
            srcDir 'src/integrationTest/resources'
        }
        compileClasspath += sourceSets.main.runtimeClasspath
    }
}

task integrationTests(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath += sourceSets.integrationTest.runtimeClasspath
}

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-all:1.10.19'

    integrationTestCompile 'junit:junit:4.12'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

jacocoTestReport {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport